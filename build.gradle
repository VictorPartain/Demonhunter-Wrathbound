plugins {
	id 'fabric-loom' version '1.9-SNAPSHOT'
	id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
	maven { url = 'https://maven.kosmx.dev/' }
	maven { url = 'https://maven.shedaniel.me/' }
	maven { url = 'https://jitpack.io' }
	maven {
		name = 'Modrinth'
		url = 'https://api.modrinth.com/maven'
		content {
			includeGroup 'maven.modrinth'
		}
	}
	maven {
		url = "https://www.cursemaven.com"
		content {
			includeGroup "curse.maven"
		}
	}
	maven {
		name = "TerraformersMC"
		url = "https://maven.terraformersmc.com/"
	}
	maven {
		name = "Ladysnake Libs"
		url = 'https://maven.ladysnake.org/releases'
	}
	maven { url 'https://maven.azuredoom.com/mods' }
}

fabricApi {
	configureDataGeneration()
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	include(implementation("com.github.LlamaLad7:MixinExtras:${project.mixin_extras_version}"))
	annotationProcessor("com.github.LlamaLad7:MixinExtras:${project.mixin_extras_version}")
	implementation include("com.github.ZsoltMolnarrr:TinyConfig:${project.tiny_config_version}")

	modImplementation("dev.kosmx.player-anim:player-animation-lib-fabric:${project.player_anim_version}")
	modApi("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}") {
		exclude(group: "net.fabricmc.fabric-api")
	}

	modImplementation("maven.modrinth:spell-engine:${project.spell_engine_version}")
	modImplementation("maven.modrinth:more-rpg-classes:${project.more_rpg_classes_version}")
	modImplementation("maven.modrinth:wizards:${project.wizards_version}")
	modImplementation("dev.emi:trinkets:${project.trinkets_version}")
	modImplementation("maven.modrinth:spell-power:${project.spell_power_version}-fabric")
	include(implementation(annotationProcessor("io.github.llamalad7:mixinextras-fabric:0.2.0-rc.4")))
	modImplementation("mod.azure.azurelibarmor:azurelibarmor-fabric-${project.azure_mc_version}:${project.azure_lib_version}")
	include(modImplementation("maven.modrinth:structure-pool-api:${project.structure_pool_api_version}"))
	modImplementation("maven.modrinth:runes:${project.runes_version}")
}

sourceSets {
	main {
		java {
			srcDirs = ["src/main/java"]
		}
		resources {
			srcDirs = ["src/main/resources", "src/main/generated"]
		}
	}
}

tasks.withType(Copy).configureEach {
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

processResources {
	inputs.properties([
			"version": project.version,
			"minecraft_compat_version": project.minecraft_compat_version
	])

	filesMatching("fabric.mod.json") {
		expand([
				"version": project.version,
				"minecraft_compat_version": project.minecraft_compat_version
		])
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

java {
	withSourcesJar()
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}" }
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}
	repositories {
		// Add publishing repos if needed
	}
}
